const LOCALID_MART_EMPLOYEE = 2
const LOCALID_FOOTPRINTS_MAN = 3

mapscripts OldaleTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_OLDALE_TOWN)
		call_if_unset(FLAG_RECEIVED_POTION_OLDALE, OldaleTown_EventScript_MoveMartEmployee)
		call_if_set(FLAG_ADVENTURE_STARTED, OldaleTown_EventScript_SetOldaleState)
		call_if_unset(FLAG_ADVENTURE_STARTED, OldaleTown_EventScript_BlockWestEntrance)
	}
}

script OldaleTown_EventScript_SetOldaleState {
	setvar(VAR_OLDALE_TOWN_STATE, 1)
	return
}

script OldaleTown_EventScript_BlockWestEntrance {
	setobjectxyperm(LOCALID_FOOTPRINTS_MAN, 1, 11)
	setobjectmovementtype(LOCALID_FOOTPRINTS_MAN, MOVEMENT_TYPE_FACE_LEFT)
	return
}

script OldaleTown_EventScript_MoveMartEmployee {
	setobjectxyperm(LOCALID_MART_EMPLOYEE, 13, 14)
	setobjectmovementtype(LOCALID_MART_EMPLOYEE, MOVEMENT_TYPE_FACE_DOWN)
	return
}

script OldaleTown_EventScript_TownSign {
	msgbox(
		"Oldale Town\n"
		"“Where things start off scarce.”",
		MSGBOX_SIGN
	)
	end
}

script OldaleTown_EventScript_Girl {
	msgbox(format("Every moment you spend outside is risky.\pZombies tend to approach silently and attack you from behind."), MSGBOX_NPC)
	end
}

script OldaleTown_EventScript_MartEmployee {
	lock
	faceplayer
	goto_if_set(FLAG_RECEIVED_POTION_OLDALE, OldaleTown_EventScript_ExplainPotion)
	playbgm(MUS_FOLLOW_ME, FALSE)
	msgbox(OldaleTown_Text_IWorkAtPokemonMart, MSGBOX_DEFAULT)
	closemessage
	switch(var(VAR_FACING)) {
		case DIR_SOUTH: 
			applymovement(OBJ_EVENT_ID_PLAYER, OldaleTown_Movement_PlayerSouth)
			applymovement(LOCALID_MART_EMPLOYEE, OldaleTown_Movement_EmployeeSouth)
			waitmovement(0)
			break
		case DIR_NORTH:
			applymovement(LOCALID_MART_EMPLOYEE, OldaleTown_Movement_EmployeeNorth)
			applymovement(OBJ_EVENT_ID_PLAYER, OldaleTown_Movement_PlayerNorth)
			waitmovement(0)
			break
		case DIR_EAST:
			applymovement(OBJ_EVENT_ID_PLAYER, OldaleTown_Movement_PlayerEast)
			applymovement(LOCALID_MART_EMPLOYEE, OldaleTown_Movement_EmployeeEast)
			waitmovement(0)
			break
	}
	call(OldaleTown_EventScript_ExplainPokemonMart)
	release
	end
}

script OldaleTown_EventScript_ExplainPokemonMart {
	msgbox(OldaleTown_Text_ThisIsAPokemonMart, MSGBOX_DEFAULT)
	giveitem(ITEM_POTION)
	if (!var(VAR_RESULT)) {
		msgbox(gText_TooBadBagIsFull, MSGBOX_DEFAULT)
		fadedefaultbgm
		release
		end
	}

	msgbox(OldaleTown_Text_PotionExplanation, MSGBOX_DEFAULT)
	setflag(FLAG_RECEIVED_POTION_OLDALE)
	fadedefaultbgm
	return
}

script OldaleTown_EventScript_ExplainPotion {
	msgbox(OldaleTown_Text_PotionExplanation, MSGBOX_DEFAULT)
	release
	end
}

movement OldaleTown_Movement_EmployeeEast {
	walk_up * 7
	walk_in_place_faster_down
	step_end
}

movement OldaleTown_Movement_EmployeeSouth {
	walk_left
	walk_up * 2
	walk_right
	walk_up * 5
	walk_in_place_faster_down
	step_end
}

movement OldaleTown_Movement_EmployeeNorth {
	walk_up * 7
	walk_in_place_faster_down
	step_end
}

movement OldaleTown_Movement_PlayerEast {
	walk_right
	walk_up * 6
	step_end
}

movement OldaleTown_Movement_PlayerSouth {
	delay_16 * 4
	walk_up * 5
	step_end
}

movement OldaleTown_Movement_PlayerNorth {
	walk_up * 7
	step_end
}

script OldaleTown_EventScript_FootprintsMan {
	lock
	faceplayer
	if (flag(FLAG_ADVENTURE_STARTED)) {
		msgbox(OldaleTown_Text_FinishedSketchingFootprints, MSGBOX_DEFAULT)
		release
		end
	}
	msgbox(OldaleTown_Text_DiscoveredFootprints, MSGBOX_DEFAULT)
	closemessage
	applymovement(LOCALID_FOOTPRINTS_MAN, Common_Movement_FaceOriginalDirection)
	waitmovement(0)
	release
	end
}

script OldaleTown_EventScript_BlockedPath {
	lockall
	applymovement(OBJ_EVENT_ID_PLAYER, OldaleTown_Movement_PlayerStepBack)
	applymovement(LOCALID_FOOTPRINTS_MAN, OldaleTown_Movement_BackUp)
	waitmovement(0)
	msgbox(OldaleTown_Text_WaitDontComeInHere, MSGBOX_DEFAULT)
	closemessage
	applymovement(LOCALID_FOOTPRINTS_MAN, OldaleTown_Movement_ReturnToOriginalPosition)
	waitmovement(0)
	releaseall
	end
}

movement OldaleTown_Movement_PlayerStepBack {
	delay_8
	walk_right
	step_end
}

movement OldaleTown_Movement_BackUp {
	walk_fast_up
	walk_in_place_faster_left
	lock_facing_direction
	walk_right
	unlock_facing_direction
	step_end
}

movement OldaleTown_Movement_ReturnToOriginalPosition {
	walk_down
	walk_left
	step_end
}

text OldaleTown_Text_IWorkAtPokemonMart {
	format("Hi!\nI work at a Pokémon Mart.\p"
	"Can I get you to come with me?")
}

text OldaleTown_Text_ThisIsAPokemonMart {
	format("This is a Pokémon Mart.\n"
	"Just look for our blue roof.\p"
	"We sell a variety of goods including Poké Balls for catching Pokémon.\p"
	"Here, I'd like you to have this as a promotional item.")
}

text OldaleTown_Text_PotionExplanation {
	format("A Potion can be used anytime, so it's even more useful than a Pokémon Center in certain situations.")
}

text OldaleTown_Text_WaitDontComeInHere {
	format("Aaaaah! Wait!\nPlease don't come in here.\p"
	"I'm on guard duty and you don't look tough enough to be let out on your own!\p"
	"So, stay away!")
}

text OldaleTown_Text_DiscoveredFootprints {
	format("I'm on guard duty and you don't look tough enough to be let out on your own!\p"
	"So, stay away!")
}

text OldaleTown_Text_FinishedSketchingFootprints {
	format("I finished sketching the footprints of a rare Pokémon.\p"
	"But it turns out they were only my own footprints…")
}

