mapscripts LittlerootTown_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		setflag(FLAG_VISITED_LITTLEROOT_TOWN)
		end
	}
}

script LittlerootTown_EventScript_BirchsLabSign {
	msgbox("Prof. Birch's Pokémon Lab", MSGBOX_SIGN)
	end
}

script LittlerootTown_EventScript_BrendansHouseSign {
	msgbox("{PLAYER}'s House", MSGBOX_SIGN)
	end
}

script LittlerootTown_EventScript_TownSign {
	msgbox("Littleroot Town\n“A town that can't be shaded any hue.”", MSGBOX_SIGN)
	end
}

script LittlerootTown_EventScript_MaysHouseSign {
	msgbox("Prof. Birch's House", MSGBOX_SIGN)
	end
}

script LittlerootTown_EventScript_BirchsBag {
	lock
	faceplayer
	if (flag(FLAG_SYS_POKEMON_GET)) {
		msgbox(format("You shouldn't steal more than what you need."), MSGBOX_NPC)
		end
	}
	setrespawn(HEAL_LOCATION_LITTLEROOT_TOWN)
	setflag(FLAG_SYS_POKEMON_GET)
	fadescreen(FADE_TO_BLACK)
	special(ChooseStarter)
	waitstate
	release
	end
}

script LittlerootTown_EventScript_WalkBackFromRoute {
	lock
	if (!flag(FLAG_SYS_POKEMON_GET)) {
		msgbox(format("I may not remember much, but I do remember that there are wild Pokemon in the grass!\pIt isn't safe without a Pokemon of my own."), MSGBOX_NPC)
		applymovement(OBJ_EVENT_ID_PLAYER, LittlerootTown_Movement_WalkBackFromRoute)
		waitmovement(0)
	}
	release
	end
}

movement LittlerootTown_Movement_WalkBackFromRoute {
	walk_down
	step_end
}