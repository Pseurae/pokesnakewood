const LOCALID_BIRCH = 1
const LOCALID_ZYOUNGSTER = 2

mapscripts Route101_MapScripts {
	MAP_SCRIPT_ON_FRAME_TABLE [
		VAR_ROUTE101_STATE, 0: Route101_EventScript_HideMapNamePopup
	]
}


script Route101_EventScript_HideMapNamePopup {
	setflag(FLAG_HIDE_MAP_NAME_POPUP)
	setvar(VAR_ROUTE101_STATE, 1)
	end
}

script Route101_EventScript_StartBirchRescue {
	lockall
	playbgm(MUS_HELP, TRUE)
	special(DisableWildEncounters)
	msgbox("{COLOR BLUE}AHHHH! He-Help! Someone!", MSGBOX_DEFAULT)
	closemessage
	setobjectxy(LOCALID_BIRCH, 0, 15)
	setobjectxy(LOCALID_ZYOUNGSTER, 0, 16)
	initcutscene
	applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_EnterScene)
	applymovement(LOCALID_BIRCH, Route101_Movement_BirchRunAway1)
	applymovement(LOCALID_ZYOUNGSTER, Route101_Movement_ZigzagoonChase1)
	waitmovement(0)
	applymovement(LOCALID_ZYOUNGSTER, Route101_Movement_ZigzagoonChaseInCircles)
	applymovement(LOCALID_BIRCH, Route101_Movement_BirchRunInCircles)
	waitmovement(0)
	applymovement(LOCALID_BIRCH, Common_Movement_WalkInPlaceFasterRight)
	waitmovement(0)
	applymovement(LOCALID_ZYOUNGSTER, Route101_Movement_ZigzagoonFaceBirch)
	applymovement(LOCALID_BIRCH, Route101_Movement_BirchFaceZigzagoon)
	waitmovement(0)
	msgbox("{COLOR BLUE}Hey-you! Help me out here!", MSGBOX_DEFAULT)
	closemessage
	exitcutscene
	setvar(VAR_ROUTE101_STATE, 2)
	releaseall
	end
}

movement Route101_Movement_PreventExitSouth {
	walk_up
	step_end
}

movement Route101_Movement_PreventExitWest {
	walk_right
	step_end
}

movement Route101_Movement_PreventExitNorth {
	walk_down
	step_end
}

movement Route101_Movement_ZigzagoonChaseInCircles {
	walk_fast_up * 3
	walk_fast_right * 3
	walk_fast_down * 2
	walk_fast_left * 3
	walk_fast_up * 2
	walk_fast_right * 3
	walk_fast_down * 2
	walk_fast_left
	walk_fast_left * 2
	walk_fast_up * 2
	walk_fast_right * 3
	walk_fast_down * 3
	walk_fast_left * 1
	step_end
}

movement Route101_Movement_BirchRunInCircles {
	walk_fast_up * 2
	walk_fast_right * 3
	walk_fast_down * 2
	walk_fast_left * 3
	walk_fast_up * 2
	walk_fast_right * 3
	walk_fast_down * 2
	walk_fast_left * 3
	walk_fast_up * 2
	walk_fast_right * 3
	walk_fast_down * 3
	walk_fast_left * 2
	step_end
}

movement Route101_Movement_ZigzagoonChase1 {
	walk_fast_up
	walk_fast_right * 4
	walk_fast_up
	step_end
}

movement Route101_Movement_ZigzagoonFaceBirch {
	walk_in_place_fast_left * 4
	step_end
}

movement Route101_Movement_EnterScene {
	walk_fast_up * 4
	walk_in_place_faster_left
	step_end
}

movement Route101_Movement_BirchRunAway1 {
	walk_fast_right * 4
	walk_fast_up * 2
	step_end
}

movement Route101_Movement_BirchFaceZigzagoon {
	walk_in_place_fast_right * 4
	step_end
}

script Route101_EventScript_RouteSign {
	msgbox("Route 101\n{UP_ARROW} Oldale Town", MSGBOX_SIGN)
	end
}

script Route101_EventScript_PreventExitSouth {
	lockall
	msgbox(Route101_Text_DontLeaveMe, MSGBOX_DEFAULT)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitSouth)
	waitmovement(0)
	releaseall
	end
}

script Route101_EventScript_PreventExitWest {
	lockall
	msgbox(Route101_Text_DontLeaveMe, MSGBOX_DEFAULT)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitWest)
	waitmovement(0)
	releaseall
	end
}

script Route101_EventScript_PreventExitNorth {
	lockall
	msgbox(Route101_Text_DontLeaveMe, MSGBOX_DEFAULT)
	closemessage
	applymovement(OBJ_EVENT_ID_PLAYER, Route101_Movement_PreventExitNorth)
	waitmovement(0)
	releaseall
	end
}

script Route101_Movement_BirchApproachPlayer {
	walk_right
	step_end
}

text Route101_Text_DontLeaveMe {
	"Wh-Where are you going?!\n"
	"Don't leave me like this!"
}

script Route101_EventScript_ZombieBoy {
	lock
	faceplayer
	setflag(FLAG_SYS_POKEMON_GET)
	setflag(FLAG_RESCUED_BIRCH)
	release
	trainerbattle_single(TRAINER_ZOMBIE_ROUTE_101, "Grrrrr...", "Grrrrr...", Route101_EventScript_ZombieBoyDefeat)
	end
}

script Route101_EventScript_ZombieBoyDefeat {
	msgbox("The zombie turned to dust.", MSGBOX_AUTOCLOSE)
	removeobject(LOCALID_ZYOUNGSTER)
	applymovement(LOCALID_BIRCH, Route101_Movement_BirchApproachPlayer)
	waitmovement(0)

	msgbox(format("{COLOR DARK_GRAY}Prof. Birch:{COLOR BLUE} Whew…\pThanks {PLAYER}! I would have been a goner if you hadn’t come along."))
	msgbox(format("{COLOR DARK_GRAY}{PLAYER}:{PLAYER_COLOR} What was that thing!? And how do you even know my name?"))
	msgbox(format("{COLOR DARK_GRAY}Prof. Birch:{COLOR BLUE} It’s me, Prof. Birch. We’ve known each other for years!\p"
		"Also don't you remember the invasion of Hoenn by these monsters?"))

	msgbox(format("{COLOR DARK_GRAY}{PLAYER}:{PLAYER_COLOR} Oh, right. You see, I just woke up in that ruined town south of here, and realised that I had lost my memory."))
	
	msgbox(format("{COLOR DARK_GRAY}Prof. Birch:{COLOR BLUE} So, it’s true then? Littleroot has been destroyed?"
		"You don't remember, but it was our home, {PLAYER}.\p"
		"Was anyone else there when you woke up? My daughter? Your brother?"))

	msgbox(format("{COLOR DARK_GRAY}{PLAYER}:{PLAYER_COLOR} No."))

	msgbox(format("{COLOR DARK_GRAY}Prof. Birch:{COLOR BLUE} Well, I daresay that you’ll get your memories back sooner or later. Meanwhile, we gotta get out of here."))
	msgbox(format("{COLOR BLUE}There’s a barricaded town to the north of here. Follow me."))
	closemessage

	special(HealPlayerParty)
	setflag(FLAG_HIDE_ROUTE_101_BIRCH_ZIGZAGOON_BATTLE)
	clearflag (FLAG_HIDE_LITTLEROOT_TOWN_BIRCHS_LAB_BIRCH)
	setflag(FLAG_HIDE_BIRCH_STARTERS_BAG)
	setvar(VAR_BIRCH_LAB_STATE, 2)
	setvar(VAR_ROUTE101_STATE, 3)
	clearflag(FLAG_HIDE_MAP_NAME_POPUP)
	special(EnableWildEncounters)
	warp(MAP_OLDALE_TOWN_POKEMON_CENTER_1F, 7, 5)
	waitstate
	release
	end
}
